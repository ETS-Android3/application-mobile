plugins {
    id 'com.android.application'
    id 'com.github.triplet.play' version '2.2.0'
}

// Load release keystore
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

// Configure the google play automatic release
play {
    serviceAccountCredentials = rootProject.file(keystoreProperties['playKeysFile'])
    track = 'alpha'
    defaultToAppBundles = true
}

android {
    compileSdkVersion 27

    signingConfigs {
        release {
            storeFile rootProject.file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    defaultConfig {
        applicationId "fr.celiangarcia.milobella"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 2
        versionName "0.1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            versionNameSuffix "-debug"
        }
    }
}

dependencies {
    implementation project(':aars')
    implementation project(':models')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.volley:volley:1.1.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

task printVersionCode {
    doLast {
        print android.defaultConfig.versionCode
    }
}

task computeNextVersionCode {
    doLast {
        print android.defaultConfig.versionCode + 1
    }
}

task printVersionName {
    doLast {
        print android.defaultConfig.versionName
    }
}

task computeNextVersionName {
    doLast {
        def m
        if ((m = android.defaultConfig.versionName =~ /(\d+)\.(\d+)\.(\d+)/)) {
            def major = m.group(1) as Integer
            def minor = (m.group(2) as Integer) + 1
            def fix = 0
            print "${major}.${minor}.${fix}"
        }
    }
}
